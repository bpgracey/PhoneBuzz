apply plugin: 'scala'

buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath 'com.github.jengelman.gradle.plugins:shadow:0.8'
    }
}

apply plugin: 'shadow'
apply plugin: 'maven'

group = 'com.bpgracey.phonebuzz'
version = '0.0.1-SNAPSHOT'

manifest.mainAttributes("Main-Class" : "com.bpgracey.phonebuzz.Rest")

description = """PhoneBuzz: FizzBuzz by phone!"""

sourceCompatibility = 1.7
targetCompatibility = 1.7

// versioning
project.ext.scala          = '2.10.4'
project.ext.scalaM         = '2.10'
project.ext.akka           = '2.3.2'
project.ext.spray          = '1.3.1'
project.ext.config         = '1.2.1'
project.ext.testkit        = '2.3.11'  // for Specs2
project.ext.slf4j          = '1.7.7'
project.ext.scalaLog       = '2.1.2'
project.ext.log            = '1.2.17'
project.ext.twilio         = '3.4.5'

repositories {
	mavenLocal()
	mavenCentral()
}

dependencies {
	
	// Scala
    compile group: 'org.scala-lang',             name: 'scala-library',               version: scala
	// Akka
    compile group: 'com.typesafe.akka',          name: "akka-actor_$scalaM",          version: akka
    compile group: 'com.typesafe.akka',          name: "akka-slf4j_$scalaM",          version: akka
	// Spray
    compile group: 'io.spray',                   name: 'spray-can',                   version: spray
    compile group: 'io.spray',                   name: 'spray-routing',               version: spray
	// Twilio
	compile group: 'com.twilio.sdk',             name: 'twilio-java-sdk',             version: twilio
	// Logging
    compile group: 'com.typesafe.scala-logging', name: "scala-logging-slf4j_$scalaM", version: scalaLog
    compile group: 'org.slf4j',                  name: 'slf4j-api',                   version: slf4j
    compile group: 'org.slf4j',                  name: 'slf4j-log4j12',               version: slf4j
    compile group: 'log4j',                      name: 'log4j',                       version: log
	// Typesafe config
    compile group: 'com.typesafe',               name: 'config',                      version: config
	
	// Test - Akka
    testCompile group: 'com.typesafe.akka', name: "akka-testkit_$scalaM", version: akka
	// Test - Spray
    testCompile group: 'io.spray',          name: 'spray-testkit',        version: spray
	// Test - Scala (may change!)
    testCompile group: 'org.specs2',        name: "specs2_$scalaM",       version: testkit

}

tasks.withType(ScalaCompile) {
	// use Zinc (typesafe incremental compiler)
    scalaCompileOptions.useAnt = false
}

import com.github.jengelman.gradle.plugins.shadow.transformers.AppendingTransformer
shadow {
	baseName = "PhoneBuzz"
	outputFile = file("$destinationDir/$baseName.$extension")
	exclude 'META-INF/*.DSA'
	exclude 'META-INF/*.RSA'
	transformer(AppendingTransformer) {
		resource = 'reference.conf'
	}
}
